// Apply this script to add ability to publish the needed artifacts.

String repositoryUserName = null
String repositoryUserPassword = null

final Properties properties = new Properties()
final File credentialsFile = project.file(credentialsPropertyFile)
if (credentialsFile.exists()) {
    properties.load(credentialsFile.newDataInputStream())
    repositoryUserName = properties.getProperty("user.name")
    repositoryUserPassword = properties.getProperty("user.password")
}

task checkPublishingCredentials << {
    if (repositoryUserName == null || repositoryUserPassword == null) {
        throw new InvalidUserDataException("Please set up valid credentials. " +
                "Credentials must be set in '${credentialsPropertyFile}' file in the project\'s root.")
    }
    println "Publishing build as ${repositoryUserName}"
}

task publish << {}

void dependPublish(final Project project) {
    final Set<Task> credentialsTasks = getTasksByName("readPublishingCredentials", false)
    project.getTasksByName("publish", false).each { final task ->
        task.dependsOn credentialsTasks
    }
    publish.dependsOn project.getTasksByName("publish", false)
}

projectsToPublish.each {
    project(":$it") { final currentProject ->
        apply plugin: 'maven-publish'

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = "${group}"
                    artifactId = "${currentProject.name}"
                    version = "${currentProject.version}"

                    from components.java

                    artifact sourceJar
                    artifact testOutputJar
                }
            }
        }

        publishing {
            repositories {
                maven {
                    url = 'http://maven.teamdev.com/repository/spine'
                    credentials {
                        username = "${repositoryUserName}"
                        password = "${repositoryUserPassword}"
                    }
                }
            }
        }

        dependPublish(project)
    }
}
