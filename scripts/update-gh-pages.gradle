import com.google.common.base.Preconditions
import com.google.common.io.Files

import java.util.regex.Pattern


/*
 * Copyright 2017, TeamDev Ltd. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'com.google.guava', name: 'guava', version: guavaVersion
    }
}

apply from: "$rootDir/scripts/no-internal-javadoc.gradle"

ext {
    javaDocFolder = Files.createTempDir()
}

/**
 * Copies the JavaDoc produced by {@code :noInternalJavadoc} task into a temporary folder.
 */
task copyJavaDoc(type: Copy) {
    from noInternalJavadoc
    into javaDocFolder
}

/**
 * Updates the JavaDoc documentation on the {@code gh-pages} Git branch.
 *
 * <p>Run this task when it is required to update the JavaDoc e.g. when merging a branch into
 * {@code master}.
 */
task updateGitHubPages {
    description "Updates the JavaDoc published to GitHub Pages Web site."
    dependsOn copyJavaDoc
}

/**
 * Waits for the given {@code process} to finish and retrieves the result of execution.
 *
 * @param process the process to execute
 * @return a tuple of format: [exitCode:<Process exit code integer>,
 *                             out: <Process stdout output as a String>,
 *                             error: <Process stderr output as a String>]
 */
static def executeForResult(final Process process) {
    final def outWriter = new StringWriter()
    final def errWriter = new StringWriter()
    process.consumeProcessOutputStream(outWriter)
    process.consumeProcessErrorStream(errWriter)
    final int exitCode = process.waitFor()
    final def result = [out: outWriter.toString(), error: errWriter.toString(), exitCode: exitCode]
    return result
}

/**
 * Executes the given command and retrieves the command output.
 *
 * <p>{@link Runtime#exec(String[], String[], File) Executes} the given {@code String} array as
 * a CLI command. If the execution is successful, returns the command output. Thrown
 * an {@link IllegalStateException} otherwise.
 *
 * @param command the command to execute
 * @return the command line output
 * @throws IllegalStateException upon an execution error
 */
String execute(final String... command) {
    final Runtime runtime = Runtime.getRuntime()
    final Process proc = runtime.exec(command, /*env=*/(String[]) null, project.rootDir)
    final def result = executeForResult(proc)
    if (result.exitCode == 0) {
        return result.out
    } else {
        final String errorMsg = "Command `$command` finished with exit code $result.exitCode:" +
                " ${System.lineSeparator()}${result.error}"
        throw new IllegalStateException(errorMsg)
    }
}

updateGitHubPages.doLast {
    final def GH_PAGES_BRANCH = "gh-pages"
    final def CURRENT_BRANCH_PATTERN = Pattern.compile("(?<=\\*\\s).*")
    final def localBranches = execute("git", "branch")
    final def matcher = CURRENT_BRANCH_PATTERN.matcher(localBranches)
    Preconditions.checkState(matcher.find())
    final def currentBranch = matcher.group()

    logger.debug("At branch `$currentBranch`")

    execute "git", "fetch", "--unshallow"
    execute "git", "checkout", GH_PAGES_BRANCH
    final def javaDocDirPostfix = "javadoc/$project.name"
    final def javaDocDir = "$rootDir/$javaDocDirPostfix" as File
    logger.debug("Updating JavaDoc on GitHub Pages in directory `$javaDocDir`")
    delete(javaDocDir)
    javaDocDir.mkdir()

    copy {
        from javaDocFolder
        into javaDocDir
    }

    execute "git", "add", javaDocDirPostfix
    execute "git", "commit", "--message=\"Update JavaDoc for module $project.name as for version $project.version\""
    execute "git", "push"
    execute "git", "checkout", currentBranch
    logger.debug("Updated JavaDoc on GitHub Pages in directory `$javaDocDir` sucessfully")
}
