dependencies {
    compile group: 'io.grpc', name: 'grpc-all', version: "$GRPC_VERSION"

    compile project(path: ':client')
    compile project(path: ':values')

    testCompile project(path: ":client", configuration: 'testArtifacts')
}

sourceSets {
    main {
        proto {
            srcDirs += ["$projectDir/src/main/proto"]
        }
        java {
            srcDir "$projectDir/generated/main/grpc"
        }
    }
}

protobuf {
    generateProtoTasks {
        all().each { final task ->
            task.plugins {
                grpc {}
                task.generateDescriptorSet = true
                task.descriptorSetOptions.path = "$projectDir/build/descriptors/${task.sourceSet.name}.desc"
                task.descriptorSetOptions.includeImports = true
            }
        }
    }
}

// required for usage of test classes from other modules
configurations {
    testArtifacts.extendsFrom testRuntime
}
task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}
artifacts {
    testArtifacts testJar
}

apply plugin: SPINE_PROTOBUF_PLUGIN_ID
