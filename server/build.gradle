buildscript {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
    dependencies {
        classpath group: 'org.spine3.tools', name: 'protobuf-plugin', version: "$SPINE_PROTOBUF_PLUGIN_VERSION", changing: true
    }
}

dependencies {
    compile group: 'io.grpc', name: 'grpc-all', version: "$GRPC_VERSION"

    compile project(path: ':client');
    compile project(path: ':values');

    testCompile project(path: ":client", configuration: 'testArtifacts')
}

apply plugin: 'org.spine3.tools.protobuf-plugin';

sourceSets {
    main {
        proto {
            srcDirs += ["${project(":server").projectDir}/src/main/proto"]
        }
        java {
            srcDir "$projectDir/generated/main/grpc"
        }
    }
}

protobuf {
    generateProtoTasks {
        all().each { final task ->
            task.plugins {
                grpc {}
                task.generateDescriptorSet = true
                task.descriptorSetOptions.path = "${projectDir}/build/descriptors/${task.sourceSet.name}.desc"
                task.descriptorSetOptions.includeImports = true
            }
        }
    }
}

// required for usage of test classes from other modules
configurations {
    testArtifacts.extendsFrom testRuntime
}
task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}
artifacts {
    testArtifacts testJar
}
