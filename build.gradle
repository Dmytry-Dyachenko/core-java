
ext {
    // NOTE: when updating Protobuf dependency, please check that KnownTypes.addStandardProtobufTypes() method
    // is updated with new Message types introduced in the new version of Protobuf.
    protobufVersion = '3.0.0'
    protobufDependency = "com.google.protobuf:protoc:${protobufVersion}"
    gRpcVersion = '1.0.0-pre2'
    slf4JVersion = '1.7.21'
    jUnitVersion = '4.12'

    spineProtobufPluginId = 'org.spine3.tools.protobuf-plugin'
    publishPlugin = "$projectDir/gradle/plugins/publish.gradle"

    credentialsPropertyFile = 'credentials.properties'
    projectsToPublish = ["client", "server", "values", "testutil"]
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'http://maven.teamdev.com/repository/spine'
        }
    }
    dependencies {
        classpath group: 'com.google.guava', name: 'guava', version: '19.0'
        classpath (group: 'com.google.protobuf', name:'protobuf-gradle-plugin', version: '0.7.7') {
            // exclude an old Guava version
            exclude group: 'com.google.guava'
        }
        classpath group: 'org.spine3.tools', name: 'protobuf-plugin', version: '1.4.10', changing: true
    }
    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'org.spine3'
    version = '0.5.5-SNAPSHOT'
}

subprojects {

    project.ext {
        sourcesRootDir = "$projectDir/src"
        generatedRootDir = "$projectDir/generated"

        generatedJavaDir = "$generatedRootDir/main/java"
        generatedTestJavaDir = "$generatedRootDir/test/java"

        generatedGrpcDir = "$generatedRootDir/main/grpc"
        generatedTestGrpcDir = "$generatedRootDir/test/grpc"

        generatedSpineDir = "$generatedRootDir/main/spine"

        testArtifactsScript = '../gradle/plugins/test-artifacts.gradle'
        generateDescriptorSetScript = '../gradle/plugins/generate-descriptor-set.gradle'
        gRpcScript = '../gradle/plugins/grpc.gradle'
        jacocoScript = '../gradle/plugins/jacoco.gradle'
    }

    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'
    apply from: jacocoScript

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    // Set Java home to point to JDK7 in gradle.properties file.
    //
    // For Mac OS X, it looks like this:
    //
    // # suppress inspection "UnusedProperty"
    // org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk1.7.0_80.jdk/Contents/Home/

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: '19.0'
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

        //As a Library, we provide logging facade API, not specific logger bindings.
        //Target apps are free to use any binding they need.
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4JVersion

        compile group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
        compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

        //Use jdk14 bindings for test purposes only.
        testCompile group: 'org.slf4j', name: 'slf4j-jdk14', version: slf4JVersion
        testCompile(group: 'junit', name: 'junit', version: jUnitVersion) {
            exclude(module: 'hamcrest-core')
        }
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.+'
    }

    sourceSets {
        main {
            proto.srcDirs = ["$sourcesRootDir/main/proto"]
            java.srcDirs = [generatedJavaDir, "$sourcesRootDir/main/java"]
            resources.srcDirs = ["$generatedRootDir/main/resources"]
        }
        test {
            proto.srcDirs = ["$sourcesRootDir/test/proto"]
            java.srcDirs = [generatedTestJavaDir, "$sourcesRootDir/test/java"]
            resources.srcDirs = ["$generatedRootDir/test/resources"]
        }
    }

    protobuf {
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$gRpcVersion"
            }
        }
        generatedFilesBaseDir = generatedRootDir
        protoc {
            artifact = protobufDependency
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }

    idea.module.iml {
        beforeMerged { final module ->
            module.dependencies.clear()
        }
        whenMerged { final module ->
            module.dependencies*.exported = true
        }
    }

    idea.module {
        generatedSourceDirs += file(generatedJavaDir)
        testSourceDirs += file(generatedTestJavaDir)
    }
}

idea.project.ipr {
    beforeMerged { final project ->
        project.modulePaths.clear()
    }
    withXml { final provider ->
        provider.node.component
                .find { it.@name == 'VcsDirectoryMappings' }
                .mapping.@vcs = 'Git'
    }
}

apply from: publishPlugin
