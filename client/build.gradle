buildscript {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
    dependencies {
        classpath group: 'org.spine3.tools', name: 'protobuf-plugin', version: "$SPINE_PROTOBUF_PLUGIN_VERSION", changing: true
    }
}

dependencies {
    compile group: 'io.grpc', name: 'grpc-all', version: "$GRPC_VERSION"
    testCompile project(path: ":testutil", configuration: 'testArtifacts')
}

apply plugin: 'org.spine3.tools.protobuf-plugin';

sourceSets {
    main {
        proto {
            srcDirs += ["${project(":client").projectDir}/src/main/proto"]
        }
        java {
            srcDir "$projectDir/generated/main/grpc"
        }
    }
}

protobuf {
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$GRPC_VERSION"
        }
    }
    generateProtoTasks {
        all().each { final task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

// required for usage of test classes from other modules
configurations {
    testArtifacts.extendsFrom testRuntime
}
task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}
artifacts {
    testArtifacts testJar
}
